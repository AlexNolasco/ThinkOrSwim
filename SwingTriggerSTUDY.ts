# SWINGTRIGGER
# DREWGRIFFITH15 (C) 2014

DECLARE UPPER;
INPUT KPERIOD = 40;
INPUT DPERIOD = 3;
INPUT SMA = 10;
INPUT EMA = 50;
INPUT MOVEMENT = 12;
INPUT OVER_BOUGHT = 85;
INPUT OVER_SOLD = 15;

# STOCHASTICSLOW
DEF FASTLINE = STOCHASTICSLOW("D PERIOD" = DPERIOD, "K PERIOD" = KPERIOD);
DEF SLOWLINE = STOCHASTICSLOW("D PERIOD" = DPERIOD, "K PERIOD" = KPERIOD).SLOWD;
DEF STOCHASTIC_SMA = SIMPLEMOVINGAVG(FASTLINE,SMA);

DEF LONG =
round(FASTLINE-FASTLINE[1],2) >= MOVEMENT
AND CLOSE >= EXPAVERAGE(CLOSE,EMA)
AND FASTLINE > STOCHASTIC_SMA
AND FASTLINE < OVER_BOUGHT
AND FASTLINE > OVER_SOLD;

DEF SHORT =
round(FASTLINE-FASTLINE[1],2) <= -MOVEMENT
AND CLOSE <= EXPAVERAGE(CLOSE,EMA)
AND FASTLINE < STOCHASTIC_SMA
AND FASTLINE > OVER_SOLD
AND FASTLINE < OVER_BOUGHT;

PLOT BULL = LONG;

PLOT BEAR = SHORT;

PLOT RATING =
IF LONG AND round(FASTLINE-FASTLINE[1],2) >= MOVEMENT THEN round(FASTLINE-FASTLINE[1],2)
ELSE IF SHORT AND round(FASTLINE-FASTLINE[1],2) <= -MOVEMENT THEN round(FASTLINE-FASTLINE[1],2)
ELSE 0;

PLOT BUY_ZONE = MOVEMENT;
BUY_ZONE.SETDEFAULTCOLOR(COLOR.DARK_GRAY);

PLOT SELL_ZONE = -MOVEMENT;
SELL_ZONE.SETDEFAULTCOLOR(COLOR.DARK_GRAY);

BULL.SETDEFAULTCOLOR(CREATECOLOR(0, 255, 0));
BULL.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_UP);
BEAR.SETDEFAULTCOLOR(CREATECOLOR(255, 0, 0));
BEAR.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_DOWN);
RATING.HIDE();
RATING.HIDEBUBBLE();
BUY_ZONE.HIDE();
BUY_ZONE.HIDEBUBBLE();
SELL_ZONE.HIDE();
SELL_ZONE.HIDEBUBBLE();
