# SWINGTRADERLX
# WGRIFFITH2 (C) 2014

# ONLY USED ON LONG POSITIONS

INPUT KPERIOD = 14;
INPUT DPERIOD = 3;
INPUT STOPLOSSLEN = 20;
INPUT RSI_LENGTH = 14;
INPUT RSIMA_LEN = 12;
INPUT SRLEN = 40;
INPUT SMOOTHINGTYPE = {DEFAULT WILDERS, EMA};

# STOCHASTICSLOW
DEF FASTLINE = STOCHASTICSLOW("D PERIOD" = DPERIOD, "K PERIOD" = KPERIOD);
DEF SLOWLINE = STOCHASTICSLOW("D PERIOD" = DPERIOD, "K PERIOD" = KPERIOD).SLOWD;

# RSI SUPPORT/RESISTANCE (SR)
DEF NETCHGAVG = WILDERSAVERAGE(CLOSE - CLOSE[1], RSI_LENGTH);
DEF TOTCHGAVG = WILDERSAVERAGE(ABSVALUE(CLOSE - CLOSE[1]), RSI_LENGTH);
DEF CHGRATIO = IF TOTCHGAVG != 0 THEN NETCHGAVG / TOTCHGAVG ELSE 0;
DEF RSI = ROUND(50 * (CHGRATIO + 1), NUMBEROFDIGITS = 0);
DEF RSI_HIGH = ROUND(HIGHEST(RSI, LENGTH = SRLEN), NUMBEROFDIGITS = 0);
DEF RSISMA = ROUND(SIMPLEMOVINGAVG(PRICE = RSI, LENGTH = RSIMA_LEN),1);

# REV ENGINEERED RSI
DEF COEFF = RSI_HIGH / (100 - RSI_HIGH);
DEF CHG = CLOSE - CLOSE[1];
DEF DIFF;
SWITCH (SMOOTHINGTYPE) {
CASE WILDERS:
    DIFF =  (RSI_LENGTH - 1) * (WILDERSAVERAGE(MAX(-CHG, 0), RSI_LENGTH) * COEFF - WILDERSAVERAGE(MAX(CHG, 0), RSI_LENGTH));
CASE EMA:
    DIFF =  (RSI_LENGTH - 1) * (EXPAVERAGE(MAX(-CHG, 0), RSI_LENGTH) * COEFF - EXPAVERAGE(MAX(CHG, 0), RSI_LENGTH)) / 2;
}

DEF VALUE = CLOSE + IF DIFF >= 0 THEN DIFF ELSE DIFF / COEFF;
DEF REVENGRSI = COMPOUNDVALUE(1, VALUE[1], DOUBLE.NAN);

DEF TARGET = HIGH >= REVENGRSI;

# TRAILINGSTOP
DEF STOP = CLOSE < LOWEST(DATA = LOW, LENGTH = STOPLOSSLEN-1)[1];

# TEST
DEF GREENPRICE = FASTLINE >= SLOWLINE AND RSI >= RSISMA;

# DEFINING ENTRY
PLOT ENTRY =
GREENPRICE
AND LOWEST(FASTLINE, 3) <= 20
AND RSISMA <= 50;

PLOT EXIT = STOP OR TARGET;

#DEF SHARES = ROUND(10000 / CLOSE);
DEF SHARES = 100;

#LONG POSITION:
ADDORDER(ORDERTYPE.BUY_TO_OPEN, ENTRY IS TRUE, TRADESIZE = SHARES, TICKCOLOR = GETCOLOR(0), ARROWCOLOR = GETCOLOR(0), NAME = "LE");
ADDORDER(ORDERTYPE.SELL_TO_CLOSE, EXIT IS TRUE, TRADESIZE = SHARES, TICKCOLOR = GETCOLOR(1), ARROWCOLOR = GETCOLOR(1), NAME = "LX", price = HIGH);

##################################################