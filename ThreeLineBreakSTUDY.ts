# THREELINEBREAK
# WGRIFFITH2 (C) 2014

## NOT IN PRODUCTION ##

DECLARE UPPER;

DEF PAINTBARS = YES;

DEF STATE = {DEFAULT BARONE, BARTWO, BARTHREE};
DEF XHIGH;
DEF XLOW;
DEF XWHITE;
SWITCH ( STATE[1] ) {
  CASE BARONE:
    STATE    = STATE.BARTWO;
    XHIGH    = HIGH;
    XLOW     = LOW;
    XWHITE   = IF CLOSE > OPEN THEN YES ELSE NO;
  CASE BARTWO:
    STATE = STATE.BARTHREE;
    IF CLOSE > XHIGH[1] THEN {
      XHIGH  = HIGH;
      XLOW   = XHIGH[1];
      XWHITE = YES;
    } ELSE IF CLOSE < XLOW[1] THEN {
      XHIGH  = XLOW[1];
      XLOW   = LOW;
      XWHITE = NO;
    } ELSE {
      XHIGH  = XHIGH[1];
      XLOW   = XLOW[1];
      XWHITE = XWHITE[1];
    }
  CASE BARTHREE:
    STATE = STATE.BARTHREE;
    IF CLOSE > MAX(XHIGH[1],XHIGH[2]) THEN {
      XHIGH  = HIGH;
      XLOW   = XHIGH[2];
      XWHITE = YES;
    } ELSE IF CLOSE < MIN(XLOW[1],XLOW[2]) THEN {
      XHIGH  = XLOW[2];
      XLOW   = LOW;
      XWHITE = NO;
    } ELSE {
      XHIGH  = XHIGH[2];
      XLOW   = XLOW[2];
      XWHITE = XWHITE[2];
    }
}

DEF TBLH = CLOSE >= XHIGH;
DEF TBLL = CLOSE <= XLOW;
DEF NEITHER = !TBLH AND !TBLL;

PLOT BULLISH = TBLH;
PLOT BEARISH = TBLL;
PLOT NEUTRAL = NEITHER;

BULLISH.SETDEFAULTCOLOR(COLOR.UPTICK);
BULLISH.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_POINTS);
BULLISH.SETLINEWEIGHT(3);
BULLISH.HIDE();
NEUTRAL.SETDEFAULTCOLOR(COLOR.BLUE);
NEUTRAL.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_POINTS);
NEUTRAL.SETLINEWEIGHT(3);
NEUTRAL.HIDE();
BEARISH.SETDEFAULTCOLOR(COLOR.DOWNTICK);
BEARISH.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_POINTS);
BEARISH.SETLINEWEIGHT(3);
BEARISH.HIDE();

DEFINEGLOBALCOLOR("BULLISH", COLOR.UPTICK);
DEFINEGLOBALCOLOR("NEUTRAL", COLOR.BLUE);
DEFINEGLOBALCOLOR("BEARISH", COLOR.DOWNTICK);
ASSIGNPRICECOLOR(IF !PAINTBARS THEN COLOR.CURRENT ELSE IF TBLH THEN GLOBALCOLOR("BULLISH") ELSE IF TBLL THEN GLOBALCOLOR("BEARISH") ELSE GLOBALCOLOR("NEUTRAL"));
