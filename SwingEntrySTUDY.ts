# SWINGENTRY
# WGRIFFITH2 (C) 2014

DECLARE UPPER;

INPUT KPERIOD = 14;
INPUT DPERIOD = 3;
INPUT RSI_LEN = 14;
INPUT RSIMA_LEN = 12;

# STOCHASTICSLOW
DEF FASTLINE = STOCHASTICSLOW("D PERIOD" = DPERIOD, "K PERIOD" = KPERIOD);
DEF SLOWLINE = STOCHASTICSLOW("D PERIOD" = DPERIOD, "K PERIOD" = KPERIOD).SLOWD;

# RSI
DEF NETCHGAVG = WILDERSAVERAGE(CLOSE - CLOSE[1], RSI_LEN);
DEF TOTCHGAVG = WILDERSAVERAGE(ABSVALUE(CLOSE - CLOSE[1]), RSI_LEN);
DEF CHGRATIO = IF TOTCHGAVG != 0 THEN NETCHGAVG / TOTCHGAVG ELSE 0;
DEF RSI = ROUND(50 * (CHGRATIO + 1), NUMBEROFDIGITS = 1);
DEF RSISMA = ROUND(SIMPLEMOVINGAVG(PRICE = RSI, LENGTH = RSIMA_LEN),1);

# TEST
DEF GREENPRICE = FASTLINE >= SLOWLINE AND RSI >= RSISMA;
DEF REDPRICE = FASTLINE < SLOWLINE AND RSI < RSISMA;

PLOT BULL =
GREENPRICE
AND RSI[1] < RSISMA[1]
AND RSISMA <= 50
AND RSISMA >= 30;

PLOT BEAR =
REDPRICE
AND RSI[1] > RSISMA[1]
AND RSISMA >= 50
AND RSISMA <= 70;

BULL.SETDEFAULTCOLOR(CREATECOLOR(0, 255, 0));
BULL.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_UP);
BEAR.SETDEFAULTCOLOR(CREATECOLOR(255, 0, 0));
BEAR.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_DOWN);