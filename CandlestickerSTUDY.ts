# CANDLESTICKER
# WGRIFFITH2 (C) 2013

INPUT LENGTH = 20;
INPUT TRENDSETUP = 3;
INPUT BODYFACTOR = 0.3;

ASSERT(BODYFACTOR >= 0, "'BODY FACTOR' MUST NOT BE NEGATIVE: " + BODYFACTOR);

DEF BODYHEIGHT = BODYHEIGHT();
DEF ISSHORT = BODYHEIGHT < BODYFACTOR * AVERAGE(BODYHEIGHT, LENGTH);
DEF BODYMAX = MAX(OPEN, CLOSE);
DEF BODYMIN = MIN(OPEN, CLOSE);
DEF ISLONGWHITE = ISLONGWHITE(LENGTH);
DEF ISLONGBLACK = ISLONGBLACK(LENGTH);
DEF ISWHITE = OPEN < CLOSE;
DEF ISBLACK = OPEN > CLOSE;
DEF ISOPENWITHINBODY = OPEN > OPEN[1] AND OPEN < CLOSE[1];
DEF ISCLOSEHIGHER = CLOSE > CLOSE[1];
DEF ISCONSUMING = BODYMAX < BODYMAX[1] AND BODYMIN > BODYMIN[1];
DEF ISENGULFING = BODYMAX > BODYMAX[1] AND BODYMIN < BODYMIN[1];

# KICKING
DEF ERRMARGIN = 0.05 * AVERAGE(BODYHEIGHT(), LENGTH);
DEF ISBLACKMARUBOZU = HIGH - OPEN <= ERRMARGIN AND
    CLOSE - LOW <= ERRMARGIN AND
    OPEN > CLOSE;
DEF ISWHITEMARUBOZU = HIGH - CLOSE <= ERRMARGIN AND
    OPEN - LOW <= ERRMARGIN AND
    OPEN < CLOSE;
DEF KICKING = ISBLACKMARUBOZU[1] AND
    ISWHITEMARUBOZU AND
    LOW > HIGH[1];
##########

# MORNINGSTAR
DEF MORNINGSTAR = ISLONGBLACK[2] AND
    (ISSHORT[1] OR ISDOJI(LENGTH)[1]) AND
    HIGH[1] < LOW[2] AND
    OPEN < CLOSE AND
    CLOSE > MIDBODYVAL()[2];
##########    

# PIERCINGLINE
DEF PIERCINGLINE = ISLONGBLACK[1] AND
    OPEN < LOW[1] AND
    CLOSE > MIDBODYVAL()[1] AND
    CLOSE < OPEN[1];
##########

# UNIQUETHREERIVERBOTTOM
DEF UNIQUETHREERIVERBOTTOM = HARAMI(LENGTH, TRENDSETUP).BULLISH[1] AND
    OPEN[1] > CLOSE[1] AND
    LOW[1] < LOW[2] AND
    ISSHORT AND
    OPEN < CLOSE AND
    CLOSE < CLOSE[1];
##########

# HARAMI
DEF HARAMI = ISLONGBLACK[1] AND
    (ISCONSUMING OR ISDOJI(LENGTH)) AND
    ISSHORT;
##########

# HOMINGPIGEON
DEF HOMINGPIGEON = ISLONGBLACK[1] AND
    OPEN > CLOSE AND
    BODYHEIGHT < BODYFACTOR * AVERAGE(BODYHEIGHT, LENGTH) AND
    OPEN < OPEN[1] AND
    CLOSE > CLOSE[1];
##########

# THREEWHITESOLIDERS
DEF THREEWHITESOLIDERS = ISLONGWHITE[2] AND
    ISLONGWHITE[1] AND
    ISLONGWHITE AND
    ISCLOSEHIGHER[1] AND
    ISCLOSEHIGHER AND
    ISOPENWITHINBODY[1] AND
    ISOPENWITHINBODY;
##########

# THREEINSIDEUP
DEF THREEINSIDEUP = HARAMI[1] AND
    OPEN < CLOSE AND
    CLOSE > CLOSE[1];
##########

# THREEOUTSIDEUP
DEF THREEOUTSIDEUP = ENGULFING(LENGTH, TRENDSETUP).BULLISH[1] AND
    OPEN < CLOSE AND
    CLOSE > CLOSE[1];
##########

# SQUEEZEALERT
DEF SQUEEZEALERT = ISLONGBLACK[2] AND
    ISCONSUMING[1] AND
    ISCONSUMING;
##########

# ONEWHITESOLIDER
DEF ONEWHITESOLIDER = ISLONGBLACK[1] AND
    ISLONGWHITE[1] AND
    OPEN > OPEN[1] AND
    CLOSE > CLOSE[2];
##########

# ENGULFING
DEF ENGULFING = ISBLACK[1] AND
    ISWHITE AND
    ISENGULFING;
##########

DEF CANDLESTICKER = KICKING OR MORNINGSTAR OR PIERCINGLINE OR
    UNIQUETHREERIVERBOTTOM OR HARAMI OR HOMINGPIGEON OR 
    THREEWHITESOLIDERS OR THREEINSIDEUP OR THREEOUTSIDEUP OR
    SQUEEZEALERT OR ONEWHITESOLIDER OR ENGULFING;

DEF RSI = RSIWilder() < 50;

DEF ENTRY = CANDLESTICKER AND RSI;
PLOT ABOVE = ENTRY;

ABOVE.SETDEFAULTCOLOR(CREATECOLOR(0, 255, 0));
ABOVE.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_UP);

#########################################