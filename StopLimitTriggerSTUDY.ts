##############STOPLMTTRIGGER###############
# WGRIFFITH2 (C) 2013
# MAJOR REVISION OF THIS STUDY ON 6/14/13
# MOVING AWAY FROM THE DARVAS BOX AND FOCUSING ON CANDLESTICK PATTERNS.
# STILL LOOKING FOR HEAVY VOLUME AND THE STOCHASTIC NEEDS TO BE IN A
# GOOD BUYING AREA OF BELOW 50.
# THE STOP WILL BE SET AT THE LOWEST PRICE OF THE LAST TWO DAYS.

INPUT SIDE = "LONG";
INPUT PERIODS = 2; # LAST NUMBER OF CANDLESTICKS

INPUT KPERIOD = 14;
INPUT DPERIOD = 3;
INPUT OVER_BOUGHT = 80;
INPUT OVER_SOLD = 20;

# STOCHASTIC SLOW CALCULATION
DEF FASTLINE = ROUND(SIMPLEMOVINGAVG(100*((CLOSE-LOWEST(LOW,KPERIOD))/(HIGHEST(HIGH,KPERIOD)-LOWEST(LOW,KPERIOD))), LENGTH = DPERIOD));
DEF SLOWLINE = ROUND(SIMPLEMOVINGAVG(SIMPLEMOVINGAVG(100*((CLOSE-LOWEST(LOW,KPERIOD))/(HIGHEST(HIGH,KPERIOD)-LOWEST(LOW,KPERIOD))), LENGTH = DPERIOD), LENGTH = DPERIOD));

DEF NEW_PERIOD = PERIODS - 1;
#HTTP://WWW.CANDLESTICKER.COM/PATTERN.ASPX?LANG=EN&PATTERN=2111
DEF ONEWHITESOLDIER = (OPEN[1]-CLOSE[1])/(.001+(HIGH[1]-LOW[1]))>0.6 AND (CLOSE-OPEN)/(.001+(HIGH-LOW))>0.6 AND (CLOSE[1]<OPEN) AND (OPEN[1] > OPEN) AND (OPEN[1]<CLOSE);
DEF CANDLESTICKER = ABANDONEDBABY().BULLISH OR ENGULFING().BULLISH OR HAMMER() OR HARAMI().BULLISH OR HARAMICROSS().BULLISH OR KICKING().BULLISH OR MARUBOZU().BULLISH OR MEETINGLINES().BULLISH OR MORNINGDOJISTAR() OR MORNINGSTAR() OR ONEWHITESOLDIER IS TRUE OR PIERCINGLINE() OR THREEINSIDEUP() OR THREEOUTSIDEUP() OR THREEWHITESOLDIERS();
DEF BUYSIGNAL = VOLUMEAVG(LENGTH = 20) > VOLUMEAVG(LENGTH = 20).VOLAVG AND FASTLINE <= 50 AND FASTLINE > SLOWLINE;
DEF ENTRY = CANDLESTICKER IS TRUE AND BUYSIGNAL IS TRUE;
DEF ROLLINGLOW = LOWEST(DATA = LOW(), LENGTH = PERIODS)[1];
DEF STOPLOSS = (LOW <= ROLLINGLOW AND ENTRY IS FALSE);

PLOT ABOVE = ENTRY;
PLOT BELOW = STOPLOSS;

BELOW.SETDEFAULTCOLOR(COLOR.DOWNTICK);
BELOW.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_DOWN);
ABOVE.SETDEFAULTCOLOR(COLOR.UPTICK);
ABOVE.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_UP);

#########################################