# STOPLMTTRIGGER
# WGRIFFITH2 (C) 2013

INPUT PERIODS = 3; 
INPUT FACTOR = 2.5; # ATR FACTOR
INPUT AVGVOL = 60;
INPUT OVERBOUGHT = 40;
INPUT PRICE = CLOSE;
INPUT AVERAGETYPE = {DEFAULT SMA, EMA};

DEF SHIFT = FACTOR * AVGTRUERANGE(HIGH, CLOSE, LOW, 20);

DEF AVERAGE;
SWITCH (AVERAGETYPE) {
CASE SMA:
    AVERAGE = AVERAGE(PRICE, 20);
CASE EMA:
    AVERAGE = EXPAVERAGE(PRICE, 20);
}

# DEFINING ENTRY
DEF KPERIOD = 14;
DEF DPERIOD = 3;
DEF FASTLINE = ROUND(SIMPLEMOVINGAVG(100 * ((CLOSE - LOWEST(LOW, KPERIOD)) / (HIGHEST(HIGH, KPERIOD) - LOWEST(LOW, KPERIOD))), LENGTH = DPERIOD));
DEF STOCH = FASTLINE > FASTLINE[1] AND FASTLINE[1] <= OVERBOUGHT;

DEF RSI = RSIWILDER()[1] <= OVERBOUGHT;

DEF MCO = (MCCLELLANOSCILLATOR()>0 OR (MCCLELLANOSCILLATOR() > ((MCCLELLANOSCILLATOR()[1]+MCCLELLANOSCILLATOR()[2])/2) AND CORRELATION(CLOSE, CLOSE("SPY"), 10)>0)) OR CORRELATION(CLOSE, CLOSE("SPY"), 10)<=0;

DEF CHANGE = CLOSE > CLOSE[1];
DEF BUYSIGNAL = VOLUMEAVG(LENGTH = AVGVOL) > VOLUMEAVG(LENGTH = AVGVOL).VOLAVG AND CHANGE IS TRUE;
DEF THREEOUT = (THREEINSIDEUP() IS TRUE OR THREEOUTSIDEUP() IS TRUE OR THREEWHITESOLDIERS() IS TRUE);
DEF ENTRY = (BUYSIGNAL IS TRUE OR THREEOUT IS TRUE) AND MCO IS TRUE AND RSI IS TRUE AND STOCH IS TRUE;

# DEFINING EXIT
DEF ROLLINGLOW = LOWEST(DATA = LOW(), LENGTH = PERIODS)[1];
DEF STOPLOSS = (LOW <= ROLLINGLOW AND ENTRY IS FALSE);
DEF TARGET = HIGH >= AVERAGE + SHIFT;
DEF EXIT = (TARGET IS TRUE OR STOPLOSS IS TRUE);

PLOT ABOVE = ENTRY;
PLOT BELOW = EXIT;

BELOW.SETDEFAULTCOLOR(CREATECOLOR(255, 0, 0));
BELOW.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_DOWN);
ABOVE.SETDEFAULTCOLOR(CREATECOLOR(0, 255, 0));
ABOVE.SETPAINTINGSTRATEGY(PAINTINGSTRATEGY.BOOLEAN_ARROW_UP);

#########################################