# SWINGTRADER
# WGRIFFITH2 (C) 2013

INPUT PERIODS = 3;
INPUT FACTOR = 2.0; # ATR FACTOR
INPUT AVGVOL = 60;
INPUT OVERBOUGHT = 40;
INPUT PRICE = CLOSE;
INPUT AVERAGETYPE = {DEFAULT SMA, EMA};

# STOCHASTICSLOW
DEF KPERIOD = 14;
DEF DPERIOD = 3;
DEF FASTLINE = ROUND(SIMPLEMOVINGAVG(100 * ((CLOSE - LOWEST(LOW, KPERIOD)) / (HIGHEST(HIGH, KPERIOD) - LOWEST(LOW, KPERIOD))), LENGTH = DPERIOD));
DEF SLOWLINE = ROUND(SIMPLEMOVINGAVG(SIMPLEMOVINGAVG(100*((CLOSE-LOWEST(LOW,KPERIOD))/(HIGHEST(HIGH,KPERIOD)-LOWEST(LOW,KPERIOD))), LENGTH = DPERIOD), LENGTH = DPERIOD));
DEF STOCH = FASTLINE > SLOWLINE AND (FASTLINE[1] <= OVERBOUGHT-20 OR FASTLINE[2] <= OVERBOUGHT-20);

# RSI
DEF RSI = REFERENCE RSIWILDER(PRICE = PRICE, LENGTH = 14);
DEF SMA = SIMPLEMOVINGAVG(PRICE = RSI, LENGTH = 12);

# MACD
DEF MACD = MACDHISTOGRAM("FAST LENGTH" = 5, "SLOW LENGTH" = 35, "MACD LENGTH" = 5);

DEF GREENPRICE = RSI > SMA AND MACD > 0 AND FASTLINE > SLOWLINE;
DEF REDPRICE = RSI < SMA AND MACD < 0 AND FASTLINE < SLOWLINE;

# DEFINING ENTRY
DEF ENTRY = VOLUMEAVG(LENGTH = AVGVOL) > VOLUMEAVG(LENGTH = AVGVOL).VOLAVG AND CLOSE > CLOSE[1] AND RSI[1] <= OVERBOUGHT AND STOCH IS TRUE;

# DEFINING EXIT
DEF ROLLINGLOW = LOWEST(DATA = LOW(), LENGTH = PERIODS)[1];
DEF STOPLOSS = (LOW <= ROLLINGLOW AND !GREENPRICE AND !ENTRY);
DEF TARGET = RSI >= 70;
DEF EXIT = STOPLOSS IS TRUE OR TARGET IS TRUE;

DEF SHARES = ROUND(10000 / CLOSE);

#LONG POSITION:
ADDORDER(ORDERTYPE.BUY_TO_OPEN, ENTRY IS TRUE, TRADESIZE = SHARES, TICKCOLOR = GETCOLOR(0), ARROWCOLOR = GETCOLOR(0), NAME = "LE");
ADDORDER(ORDERTYPE.SELL_TO_CLOSE, EXIT IS TRUE, TRADESIZE = SHARES, TICKCOLOR = GETCOLOR(1), ARROWCOLOR = GETCOLOR(1), NAME = "LX", PRICE = ohlc4());

##################################################
