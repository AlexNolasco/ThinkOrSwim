# RSI_SR
# DREWGRIFFITH15 (C) 2014

DECLARE LOWER;

INPUT RSI_LENGTH = 14;
INPUT RSI_SR = 60;
INPUT SMA_LENGTH = 12;
INPUT OVER_BOUGHT = 70;
INPUT OVER_SOLD = 30;
INPUT MOVEMENT = 10;

# RSI
DEF NETCHGAVG = WILDERSAVERAGE(CLOSE - CLOSE[1], RSI_LENGTH);
DEF TOTCHGAVG = WILDERSAVERAGE(ABSVALUE(CLOSE - CLOSE[1]), RSI_LENGTH);
DEF CHGRATIO = IF TOTCHGAVG != 0 THEN NETCHGAVG / TOTCHGAVG ELSE 0;

plot RSI = ROUND(50 * (CHGRATIO + 1), NUMBEROFDIGITS = 0);

plot RSI_HIGH = ROUND(HIGHEST(RSI, LENGTH = RSI_SR), NUMBEROFDIGITS = 0);

plot RSI_LOW = ROUND(LOWEST(RSI, LENGTH = RSI_SR), NUMBEROFDIGITS = 0);

plot RSI_SMA = ROUND(SIMPLEMOVINGAVG(PRICE = RSI, LENGTH = SMA_LENGTH),0);

PLOT RATING =
IF CLOSE CROSSES EXPAVERAGE(CLOSE,60) AND RSI == RSI_HIGH AND ROUND(RSI-RSI[1],2) >= MOVEMENT THEN ROUND(RSI-RSI[1],0)
ELSE IF CLOSE CROSSES EXPAVERAGE(CLOSE,60) AND RSI == RSI_LOW AND ROUND(RSI-RSI[1],2) <= -MOVEMENT THEN ROUND(RSI-RSI[1],0)
ELSE 0;

RSI.SETDEFAULTCOLOR(COLOR.GRAY);
RSI.SETLINEWEIGHT(3);
RSI.ASSIGNVALUECOLOR(IF RSI == RSI_LOW THEN COLOR.YELLOW ELSE IF RSI == RSI_HIGH THEN COLOR.YELLOW ELSE COLOR.GRAY);

RSI_HIGH.SETDEFAULTCOLOR(COLOR.CYAN);
RSI_HIGH.SETLINEWEIGHT(1);

RSI_LOW.SETDEFAULTCOLOR(COLOR.CYAN);
RSI_LOW.SETLINEWEIGHT(1);

RSI_SMA.SETDEFAULTCOLOR(COLOR.DARK_GRAY);
RSI_SMA.SETLINEWEIGHT(1);

PLOT OVERBOUGHT = OVER_BOUGHT;
OVERBOUGHT.SETDEFAULTCOLOR(COLOR.DARK_GRAY);

PLOT OVERSOLD = OVER_SOLD;
OVERSOLD.SETDEFAULTCOLOR(COLOR.DARK_GRAY);

RATING.HIDE();
RATING.HIDEBUBBLE();
