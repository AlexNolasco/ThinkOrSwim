# RSI_SR
# DREWGRIFFITH15 (C) 2014

DECLARE LOWER;

INPUT RSI_LENGTH = 14;
INPUT RSI_SR = 90;
INPUT SMA_LENGTH = 10;
INPUT OVER_BOUGHT = 70;
INPUT OVER_SOLD = 30;
INPUT MOVEMENT = 5;

# RSI
DEF NETCHGAVG = WILDERSAVERAGE(CLOSE - CLOSE[1], RSI_LENGTH);
DEF TOTCHGAVG = WILDERSAVERAGE(ABSVALUE(CLOSE - CLOSE[1]), RSI_LENGTH);
DEF CHGRATIO = IF TOTCHGAVG != 0 THEN NETCHGAVG / TOTCHGAVG ELSE 0;

plot RSI = ROUND(50 * (CHGRATIO + 1), NUMBEROFDIGITS = 0);

plot RSI_HIGH = ROUND(HIGHEST(RSI, LENGTH = RSI_SR), NUMBEROFDIGITS = 0);

plot RSI_LOW = ROUND(LOWEST(RSI, LENGTH = RSI_SR), NUMBEROFDIGITS = 0);

plot RSI_SMA = ROUND(SIMPLEMOVINGAVG(PRICE = RSI, LENGTH = SMA_LENGTH),0);

PLOT RATING =
IF RSI_HIGH > RSI_HIGH[13] AND ROUND(RSI_HIGH-RSI_HIGH[13],2) >= MOVEMENT AND RSI_HIGH[13] <= OVER_BOUGHT AND CLOSE > EXPAVERAGE(CLOSE,60) THEN ROUND(RSI_HIGH-RSI_HIGH[13],0)
ELSE IF RSI_LOW < RSI_LOW[13] AND ROUND(RSI_LOW-RSI_LOW[13],2) <= -MOVEMENT AND RSI_LOW[13] >= OVER_SOLD AND CLOSE < EXPAVERAGE(CLOSE,60) THEN ROUND(RSI_LOW-RSI_LOW[13],0)
ELSE 0;

RSI.SETDEFAULTCOLOR(COLOR.GRAY);
RSI.SETLINEWEIGHT(3);
RSI.ASSIGNVALUECOLOR(IF RSI == RSI_LOW THEN COLOR.YELLOW ELSE IF RSI == RSI_HIGH THEN COLOR.YELLOW ELSE COLOR.GRAY);

RSI_HIGH.SETDEFAULTCOLOR(COLOR.CYAN);
RSI_HIGH.SETLINEWEIGHT(1);

RSI_LOW.SETDEFAULTCOLOR(COLOR.CYAN);
RSI_LOW.SETLINEWEIGHT(1);

RSI_SMA.SETDEFAULTCOLOR(COLOR.DARK_GRAY);
RSI_SMA.SETLINEWEIGHT(1);

PLOT OVERBOUGHT = OVER_BOUGHT;
OVERBOUGHT.SETDEFAULTCOLOR(COLOR.DARK_GRAY);

PLOT OVERSOLD = OVER_SOLD;
OVERSOLD.SETDEFAULTCOLOR(COLOR.DARK_GRAY);

RATING.HIDE();
RATING.HIDEBUBBLE();
