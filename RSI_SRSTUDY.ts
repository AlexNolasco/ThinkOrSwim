# RSI_SR
# DREWGRIFFITH15 (C) 2014

DECLARE LOWER;

INPUT RSI_LENGTH = 14;
INPUT SMA_LENGTH = 10;
INPUT SHORT_SR = 8;
INPUT LONG_SR = 40;
INPUT OVER_BOUGHT = 70;
INPUT OVER_SOLD = 30;

# RSI
DEF NETCHGAVG = WILDERSAVERAGE(CLOSE - CLOSE[1], RSI_LENGTH);
DEF TOTCHGAVG = WILDERSAVERAGE(ABSVALUE(CLOSE - CLOSE[1]), RSI_LENGTH);
DEF CHGRATIO = IF TOTCHGAVG != 0 THEN NETCHGAVG / TOTCHGAVG ELSE 0;

plot RSI = ROUND(50 * (CHGRATIO + 1), NUMBEROFDIGITS = 0);

DEF SHORT_SR_HIGH = ROUND(HIGHEST(RSI, LENGTH = SHORT_SR), NUMBEROFDIGITS = 0);
plot LONG_SR_HIGH = ROUND(HIGHEST(RSI, LENGTH = LONG_SR), NUMBEROFDIGITS = 0);

DEF SHORT_SR_LOW = ROUND(LOWEST(RSI, LENGTH = SHORT_SR), NUMBEROFDIGITS = 0);
plot LONG_SR_LOW = ROUND(LOWEST(RSI, LENGTH = LONG_SR), NUMBEROFDIGITS = 0);

plot RSI_SMA = ROUND(SIMPLEMOVINGAVG(PRICE = RSI, LENGTH = SMA_LENGTH),0);

PLOT RATING = RSI;

RSI.SETDEFAULTCOLOR(COLOR.GRAY);
RSI.SETLINEWEIGHT(3);
RSI.ASSIGNVALUECOLOR(IF (RSI == SHORT_SR_HIGH OR RSI == SHORT_SR_LOW) THEN COLOR.YELLOW ELSE COLOR.GRAY);

LONG_SR_HIGH.SETDEFAULTCOLOR(COLOR.GRAY);
LONG_SR_HIGH.SetStyle(Curve.SHORT_DASH);
LONG_SR_HIGH.SETLINEWEIGHT(1);

LONG_SR_LOW.SETDEFAULTCOLOR(COLOR.GRAY);
LONG_SR_LOW.SetStyle(Curve.SHORT_DASH);
LONG_SR_LOW.SETLINEWEIGHT(1);

RSI_SMA.SETDEFAULTCOLOR(COLOR.DARK_GRAY);
RSI_SMA.SetStyle(Curve.SHORT_DASH);
RSI_SMA.SETLINEWEIGHT(1);

PLOT OVERBOUGHT = OVER_BOUGHT;
OVERBOUGHT.SETDEFAULTCOLOR(COLOR.DARK_GRAY);
OVERBOUGHT.HIDE();
OVERBOUGHT.HIDEBUBBLE();

PLOT OVERSOLD = OVER_SOLD;
OVERSOLD.SETDEFAULTCOLOR(COLOR.DARK_GRAY);
OVERSOLD.HIDE();
OVERSOLD.HIDEBUBBLE();

RATING.HIDE();
RATING.HIDEBUBBLE();
